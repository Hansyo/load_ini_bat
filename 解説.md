# コードの解説

bat ファイルに日本語のコメントを書いて不用意なご動作をすると面倒なので、解説と使い方はこちらに書きます。

## 使い方

次みたいな形で実行すると、ini ファイルに応じた出力が得られます。
一旦使って見ると

```bat
.\load_ini.bat [iniファイルのパス]
```

## ini ファイルの書き方

```ini
[デバイス名]
IP = xxx.xxx.xxx.xxx
PORT = xxxx
HOSTNAME = xxxxxxxx
```

## ini ファイルの例

```ini
[test1]
IP = 192.168.11.101

[test2]
IP = 192.168.11.102
PORT = 22
HOSTNAME = test2
```

### 例の実行結果

実行結果は次の通りで、TerraTerm マクロからは、=の左側が変数名、右側が変数の値として参照できるはず。

```
DEVICENAME_1=test1
test1_IP=192.168.11.101
DEVICENAME_2=test2
test2_IP=192.168.11.102
test2_PORT=22
test2_HOSTNAME=test2
TOTAL_DEVICE_NUM=2
```

## 解説

```bat
@echo off                          :: echo off でコマンドの表示をオフにするおまじない
setlocal enabledelayedexpansion    :: !var! で変数を参照できるようにするおまじない

rem Load ini file used to set variables on terraterm macro

rem Set ini file path from argv
set INI_FILE=%1                    :: 第一引数をINI_FILEに代入

rem Check ini file
if NOT EXIST %INI_FILE% (
  echo Usage: load_ini.bat [ini file path]
  exit 1
)

rem Load ini file
set DEVICE_COUNT=0
set DEVICENAME=""

rem Read ini file
:: iniファイルを1行ずつ読み込む
:: 読み込んだ行を「=」で分割して、1列目を%%aに、2列目を%%bに代入。
for /f "tokens=1,2 delims==" %%a in (%INI_FILE%) do (
  set tmp_A=%%a                                 :: 1列目をtmp_Aに代入 (!var! で変数を参照できるようにするおまじない)
  set tmp_A=!tmp_A: =!                          :: tmp_Aの先頭と末尾の空白を削除
  set tmp_B=%%b                                 :: 2列目をtmp_Bに代入
  set tmp_B=!tmp_B: =!                          :: tmp_Bの先頭と末尾の空白を削除
  if "!tmp_A!"=="[!tmp_A:~1,-1!]" (             :: 1列目が[デバイス名]の場合
    set /a DEVICE_COUNT=!DEVICE_COUNT!+1        :: DEVICE_COUNTを1増やす
    set DEVICENAME=!tmp_A:~1,-1!                :: DEVICENAMEにデバイス名を代入
    set DEVICENAME_!DEVICE_COUNT!=!DEVICENAME!  :: DEVICENAME_1, DEVICENAME_2, ... にデバイス名を代入
    echo DEVICENAME_!DEVICE_COUNT!=!DEVICENAME! :: TerraTermのマクロで使うために出力
  ) else (                                      :: 1列目が[デバイス名]以外の場合
    if "!tmp_B!"=="" (                          :: 2列目が空白の場合 -> "="で区切られていないため、iniファイルが不正
      echo Invalid ini file
      exit 1                                    :: エラー終了
    )
    set !DEVICENAME!_!tmp_A!=!tmp_B!            :: デバイス名_変数名=tmp_B という形で変数を代入
    echo !DEVICENAME!_!tmp_A!=!tmp_B!           :: TerraTermのマクロで使うために出力
  )
)
echo TOTAL_DEVICE_NUM=!DEVICE_COUNT!            :: トータルのデバイス数を出力

exit /b 0                                       :: 正常終了
```
